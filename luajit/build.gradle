import com.badlogic.gdx.jnigen.commons.Os
import com.badlogic.gdx.jnigen.BuildTarget

plugins {
    id 'java'
    id 'java-library'
}

group = rootProject.group
version = rootProject.version

configurations {
    desktopNatives {
        canBeConsumed = true
        canBeResolved = false
    }

    instrumentedJars {
        canBeConsumed = true
        canBeResolved = false
        extendsFrom api, implementation, runtimeOnly
    }
}

dependencies {
    api project(':luajava')
}

enum Platform {
    Android,
    IOS,
    // Linux
    Linux64,
    Linux32,
    ARM,
    AArch64,
    // Windows
    Win32,
    Win64,
    WinAArch64,
    // MacOsX
    MacOsX,
    MacOsXAArch64,
    MacOsXMerged, // Merged libluajit.a
}

// Builds LuaJIT and copies the output libluajit.a to jni/luajit/lib/${platform}
// libluajit.a will be used in linkerConfig
String addLuaJitTarget(Platform platform, List<String> command) {
    String name = platform.toString()
    String target = "buildLua${name}"
    String folder = "jni/luajit/lib/${name.toLowerCase()}"
    tasks.register(target) {
        if (command.get(0) == 'make') {
            doLast {
                exec {
                    workingDir 'jni/luajit'
                    commandLine 'make', 'clean'
                }
                delete {
                    delete fileTree('jni/luajit/src') {
                        include 'libluajit*a'
                    }
                }
                exec {
                    workingDir 'jni/luajit'
                    commandLine command
                }
                copy {
                    from('./jni/luajit/src') {
                        include 'libluajit*a'
                    }
                    into folder
                    rename '(.+)', 'libluajit.a'
                }
                // TODO: luajit.h is generated and could be platform-specific,
                //       although currently it does not seem to be.
            }
        } else {
            doLast {
                exec {
                    workingDir 'jni/luajit'
                    commandLine command
                }
            }
        }
    }
    return target
}

void buildLuaTargets(String... targets) {
    tasks.register('buildLua') {
        dependsOn targets
    }
    // disabling concurrency
    for (int i = 0; i < targets.length - 1; ++i) {
        for (int j = i + 1; j < targets.length; ++j) {
            tasks.named(targets[j]).get().mustRunAfter(tasks.named(targets[i]).get())
        }
    }
}

buildLuaTargets(
        addLuaJitTarget(Platform.Android,
                ['bash', '../scripts/build-android.sh']),
        addLuaJitTarget(Platform.IOS,
                ['bash', '../scripts/build-ios.sh']),
        addLuaJitTarget(Platform.Linux64,
                ['make', 'amalg',
                 'CC=gcc -m64 -D_FORTIFY_SOURCE=0', 'CFLAGS=-fPIC ',
                 'TARGET_SYS=Linux']),
        addLuaJitTarget(Platform.Linux32,
                ['make', 'amalg',
                 'HOST_CC=i686-linux-gnu-gcc -m32 -D_FORTIFY_SOURCE=0', 'CFLAGS=-fPIC',
                 'TARGET_SYS=Linux', 'CROSS=i686-linux-gnu-']),
        addLuaJitTarget(Platform.ARM,
                ['make', 'amalg',
                 'HOST_CC=i686-linux-gnu-gcc -m32 -D_FORTIFY_SOURCE=0', 'CFLAGS=-fPIC',
                 'TARGET_SYS=Linux', 'CROSS=arm-linux-gnueabihf-']),
        addLuaJitTarget(Platform.AArch64,
                ['make', 'amalg',
                 'HOST_CC=gcc -m64 -D_FORTIFY_SOURCE=0', 'CFLAGS=-fPIC',
                 'TARGET_SYS=Linux', 'CROSS=aarch64-linux-gnu-']),
        addLuaJitTarget(Platform.Win32,
                ['make', 'amalg',
                 'HOST_CC=i686-linux-gnu-gcc -m32', 'CFLAGS=-fPIC',
                 'BUILDMODE=static',
                 'TARGET_SYS=Windows', 'CROSS=i686-w64-mingw32-']),
        addLuaJitTarget(Platform.Win64,
                ['make', 'amalg',
                 'HOST_CC=gcc -m64', 'CFLAGS=-fPIC',
                 'BUILDMODE=static',
                 'TARGET_SYS=Windows', 'CROSS=x86_64-w64-mingw32-']),
        addLuaJitTarget(Platform.WinAArch64,
                ['make', 'amalg',
                 'HOST_CC=gcc -m64', 'CFLAGS=-fPIC',
                 'BUILDMODE=static',
                 'TARGET_SYS=Windows', 'CROSS=aarch64-w64-mingw32-', ]),
        addLuaJitTarget(Platform.MacOsX,
                ['make', 'amalg', 'TARGET_FLAGS=-arch x86_64',
                 'CFLAGS=-fPIC', 'TARGET_SYS=Darwin']),
        addLuaJitTarget(Platform.MacOsXAArch64,
                ['make', 'amalg', 'TARGET_FLAGS=-arch arm64',
                 'CFLAGS=-fPIC', 'TARGET_SYS=Darwin']),
)

void linkerConfig(BuildTarget it, Platform platform) {
    it.linkerFlags += "-L$projectDir/jni/luajit/lib/${platform.toString().toLowerCase()}".toString()
    if (it.os == Os.Linux) {
        it.cFlags += ['-D_FORTIFY_SOURCE=0']
        it.cppFlags += ['-D_FORTIFY_SOURCE=0']
    }
}

apply plugin: 'com.badlogicgames.jnigen.jnigen-gradle'

jnigen {
    sharedLibName = 'luajit'

    all {
        cppExcludes = ['jni/luajit/**/*']
        cExcludes = ['jni/luajit/**/*']
        libraries += ['-lm', '-lluajit']
        headerDirs = ['../jni/luajava', 'jni/luajit/src', 'jni/mod']
    }

    addLinux(x64, x86, {
        linkerConfig(it, Platform.Linux64)
    })
    addLinux(x32, x86, {
        // TODO: maybe report to jnigen
        compilerPrefix = 'i686-linux-gnu-'
        cFlags = cFlags.findAll { it != '-m32' }
        cppFlags = cppFlags.findAll { it != '-m32' }
        linkerFlags = linkerFlags.findAll { it != '-m32' }
        linkerConfig(it, Platform.Linux32)
    })
    addLinux(x32, ARM, {
        linkerConfig(it, Platform.ARM)
    })
    addLinux(x64, ARM, {
        linkerConfig(it, Platform.AArch64)
    })

    addWindows(x32, x86, {
        linkerConfig(it, Platform.Win32)
    })
    addWindows(x64, x86, {
        linkerConfig(it, Platform.Win64)
    })
    addWindows(x64, ARM) {
        linkerConfig(it, Platform.WinAArch64)
    }

    addMac(x64, x86, {
        linkerConfig(it, Platform.MacOsXMerged)
    })
    addMac(x64, ARM, {
        linkerConfig(it, Platform.MacOsXMerged)
    })

    robovm {
        forceLinkClasses "java.lang.Class", "java.lang.Throwable", "party.iroiro.luajava.JuaAPI"
        xcframeworkBundleIdentifier = "party.iroiro.luajava.luajit"
        minIOSVersion = "11.0"
    }
    addIOS {
        String directory = "${it.architecture.displayName.toLowerCase()}-${it.targetType.targetTypeBuildDirName}"
        linkerFlags += "-L${projectDir}/jni/luajit/lib/ios/${directory}".toString()
    }

    addAndroid {
        cFlags += ['-D_FORTIFY_SOURCE=1']
        cppFlags += ['-D_FORTIFY_SOURCE=1']
        androidApplicationMk = [
                'APP_PLATFORM := android-21',
                'APP_CFLAGS := -D_FORTIFY_SOURCE=1 ',
        ]
        linkerFlags += ["-L${projectDir}/jni/luajit/lib/android/\$(TARGET_ARCH_ABI)".toString(), '-lluajit']
    }
}

tasks.register('buildLuaMacOsXMerge', {
    dependsOn tasks.buildLuaMacOsX, tasks.buildLuaMacOsXAArch64
    String pathX86 = "lib/${Platform.MacOsX.toString().toLowerCase()}"
    String pathAArch64 = "lib/${Platform.MacOsXAArch64.toString().toLowerCase()}"
    String pathA = "lib/${Platform.MacOsXMerged.toString().toLowerCase()}"
    doLast {
        exec {
            workingDir 'jni/luajit'
            commandLine 'mkdir', '-p', pathA
        }
        exec {
            workingDir 'jni/luajit'
            commandLine 'lipo', '-create',
                    "${pathX86}/libluajit.a", "${pathAArch64}/libluajit.a",
                    '-output', "${pathA}/libluajit.a"
        }
    }
})

tasks.jnigenBuildLinux_x86_32.dependsOn(tasks.buildLuaLinux32)
tasks.jnigenBuildLinux_x86_64.dependsOn(tasks.buildLuaLinux64)
tasks.jnigenBuildLinux_Arm_32.dependsOn(tasks.buildLuaARM)
tasks.jnigenBuildLinux_Arm_64.dependsOn(tasks.buildLuaAArch64)
tasks.jnigenBuildAllLinux.dependsOn(
        tasks.buildLuaLinux32,
        tasks.buildLuaLinux64,
        tasks.buildLuaARM,
        tasks.buildLuaAArch64,
)

tasks.jnigenBuildWindows_x86_32.dependsOn(tasks.buildLuaWin32)
tasks.jnigenBuildWindows_x86_64.dependsOn(tasks.buildLuaWin64)
tasks.jnigenBuildWindows_Arm_64.dependsOn(tasks.buildLuaWinAArch64)
tasks.jnigenBuildAllWindows.dependsOn(
        tasks.buildLuaWin32,
        tasks.buildLuaWin64,
        tasks.buildLuaWinAArch64,
)

tasks.jnigenBuildAllMacOsX.dependsOn(tasks.buildLuaMacOsXMerge)
tasks.jnigenBuildAllAndroid.dependsOn(tasks.buildLuaAndroid)
tasks.jnigenBuildAllIOS.dependsOn(tasks.buildLuaIOS)
tasks.buildLuaIOS.dependsOn(tasks.jnigen)

void addPatchElfTask(String platform) {
    String target = "patchElf${platform}"
    String folder = "build/jnigen/libs/${platform.toLowerCase()}"
    tasks.register(target) {
        doLast {
            String file = projectDir.toPath().resolve(folder).toFile().listFiles(new FilenameFilter() {
                @Override
                boolean accept(File file, String s) {
                    return s.endsWith('.so')
                }
            })[0].toPath().toAbsolutePath().toString()
            List<String> command = new ArrayList<>()
            command.add('patchelf')
            for (String symbol : ['dlclose',
                                  'dlerror',
                                  'dlopen',
                                  'dlsym',
                                  'exp',
                                  'log',
                                  'log2',
                                  'pow']) {
                command.add('--clear-symbol-version')
                command.add(symbol)
            }
            command.add(file)
            logger.lifecycle('$ patchelf --version')
            exec {
                workingDir folder
                commandLine new ArrayList<>(['patchelf', '--version'])
            }
            logger.lifecycle('$ ' + command.join(' '))
            exec {
                workingDir folder
                commandLine command
            }
        }
    }
}

addPatchElfTask('Linux32')
addPatchElfTask('Linux64')
addPatchElfTask('LinuxARM32')
addPatchElfTask('LinuxARM64')

tasks.jnigenBuildLinux_x86_32.finalizedBy(tasks.patchElfLinux32)
tasks.jnigenBuildLinux_x86_64.finalizedBy(tasks.patchElfLinux64)
tasks.jnigenBuildLinux_Arm_32.finalizedBy(tasks.patchElfLinuxARM32)
tasks.jnigenBuildLinux_Arm_64.finalizedBy(tasks.patchElfLinuxARM64)
tasks.jnigenBuildAllLinux.finalizedBy(
        tasks.patchElfLinux32,
        tasks.patchElfLinux64,
        tasks.patchElfLinuxARM32,
        tasks.patchElfLinuxARM64,
)

artifacts {
    artifacts {
        [
                tasks.jnigenPackageAllAndroid,
                tasks.jnigenPackageAllIOS,
                tasks.jnigenPackageAllDesktop,
        ].forEach { packageTask ->
            packageTask.outputs.files.forEach { file ->
                archives(file) {
                    builtBy(packageTask)
                }
            }
        }
    }

    instrumentedJars(jar)
    desktopNatives(jnigenPackageAllDesktop.outputs.files.files) {
        builtBy(jnigenPackageAllDesktop)
    }
}

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'party.iroiro.luajava.luajit')
    }
}
