buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.badlogicgames.gdx:gdx-jnigen-gradle:2.5.1'
    }
}

plugins {
    id 'java'
    id 'java-library'
}

repositories {
    mavenCentral()
}

group = rootProject.group
version = rootProject.version

configurations {
    desktopNatives {
        canBeConsumed = true
        canBeResolved = false
    }

    instrumentedJars {
        canBeConsumed = true
        canBeResolved = false
        extendsFrom api, implementation, runtimeOnly
    }
}

dependencies {
    api project(':luajava')
}

apply plugin: 'com.badlogicgames.gdx.gdx-jnigen'

static def removeNonArmFlags(String s) {
    // TODO: Until https://github.com/libgdx/gdx-jnigen/pull/56 gets released
    return s
            .replace("-mfpmath=sse -msse2", "")
            .replace("-m32", "")
            .replace("-m64", "")
}

jnigen {
    sharedLibName = 'lua53'

    all {
        cFlags += ' -DLUA_COMPAT_5_2'
        cppFlags += ' -DLUA_COMPAT_5_2'
        headerDirs = ['../../jni/luajava', 'mod', 'lua53']
        cppExcludes = ['lua53/**/*']
        cExcludes = ['lua53/**/*']
        libraries = ' -lm '
    }

    add(Windows, x32)
    add(Windows, x64)
    add(Windows, x64, ARM) {
        cFlags = removeNonArmFlags(cFlags)
        cppFlags = removeNonArmFlags(cppFlags)
        linkerFlags = removeNonArmFlags(linkerFlags)
    }

    add(Linux, x32)
    add(Linux, x64)
    add(Linux, x32, ARM)
    add(Linux, x64, ARM)
    each({ it.os == Linux }) {
        String linuxFlags = ' -D_FORTIFY_SOURCE=0 -DLUA_USE_DLOPEN '
        cFlags += linuxFlags
        cppFlags += linuxFlags
    }

    add(MacOsX, x64)
    add(MacOsX, x64, ARM)
    each({ it.os == MacOsX }) {
        String macFlags = ' -DLUA_USE_DLOPEN '
        libraries = ''
        cFlags += macFlags
        cppFlags += macFlags
    }

    add(Android) {
        String androidFlags = ' -D_FORTIFY_SOURCE=1 -DLUA_USE_DLOPEN '
        cFlags += androidFlags
        cppFlags += androidFlags
        androidApplicationMk = [
                'APP_PLATFORM := android-21',
                "APP_CFLAG :=$androidFlags",
        ]
    }

    robovm {
        forceLinkClasses "java.lang.Class", "java.lang.Throwable", "party.iroiro.luajava.JuaAPI"
    }
    add(IOS, x64) {
        libraries = ''
        xcframeworkBundleIdentifier = "party.iroiro.luajava.lua53"
        minIOSVersion = "11.0"
    }
}

artifacts {
    instrumentedJars(jar)
    desktopNatives(jnigenJarNativesDesktop)
}

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'party.iroiro.luajava.lua53')
    }
}

tasks.jnigen.dependsOn(tasks.build)
