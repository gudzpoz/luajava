buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.badlogicgames.gdx:gdx-jnigen-gradle:2.3.1'
    }
}

plugins {
    id 'java'
    id 'java-library'
}

repositories {
    mavenCentral()
}

configurations {
    desktopNatives {
        canBeConsumed = true
        canBeResolved = false
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    implementation project(':luajava')
    implementation 'com.badlogicgames.gdx:gdx-jnigen-loader:2.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

apply plugin: 'com.badlogicgames.gdx.gdx-jnigen'

jnigen {
    sharedLibName = 'lua53'

    all {
        headerDirs = ['../../jni/luajava', 'lua53', 'mod']
        cppExcludes = ['lua53/**/*']
        cExcludes = ['lua53/**/*']
        libraries = ' -lm '
    }

    add(Windows, x32)
    add(Windows, x64)
    add(Linux, x32)
    add(Linux, x64)
    add(Linux, x32, ARM)
    add(Linux, x64, ARM)
    add(MacOsX, x64) {
        libraries = ''
    }
    add(Android) {
        androidApplicationMk = [
                'APP_PLATFORM := android-21'
        ]
    }
    add(IOS) {
        libraries = ''
    }
}

artifacts {
    desktopNatives(jnigenJarNativesDesktop)
}

tasks.jnigen.dependsOn(tasks.build)

