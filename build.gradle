group 'party.iroiro.luajava'
version '3.0.2-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

subprojects {
    apply plugin: 'java'
    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        withSourcesJar()
        withJavadocJar()
    }
}

ext {
    jdk = JavaVersion.current().majorVersion
    jdkJavadoc = "https://docs.oracle.com/javase/$jdk/docs/api/"
    if (JavaVersion.current().isJava11Compatible()) {
        jdkJavadoc = "https://docs.oracle.com/en/java/javase/$jdk/docs/api/"
    }
    println "JDK Javadoc link for this build is ${rootProject.jdkJavadoc}"
}

apply from: 'publish.gradle'
apply from: 'jacoco.gradle'

task allJavadoc(type: Javadoc) {
    source subprojects.findAll { it.name != 'example' }.collect {
        it.sourceSets.main.allJava
    }
    classpath = files(subprojects.findAll { it.name != 'example' }.collect {
        it.sourceSets.main.compileClasspath
    })
    exclude '**/party/iroiro/luajava/util/**'
    destinationDir = file("${buildDir}/docs/javadoc")
}
