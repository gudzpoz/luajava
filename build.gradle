group 'party.iroiro.luajava'
version(System.getenv('IS_RELEASE') == 'true' ? '4.0.2' : '4.0.3-SNAPSHOT')

buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath 'com.badlogicgames.jnigen:jnigen-gradle:3.1.1'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

subprojects { it ->
    repositories {
        mavenCentral()
    }

    if (!['android', 'android-test', 'jpms-example'].contains(it.name)) {
        apply plugin: 'java'

        java {
            withSourcesJar()
            withJavadocJar()
        }

        tasks.withType(JavaCompile).configureEach {
            options.release = 8
        }
    }
}

ext {
    jdk = JavaVersion.current().majorVersion
    jdkJavadoc = "https://docs.oracle.com/javase/8/docs/api/"
    println "JDK Javadoc link for this build is ${rootProject.jdkJavadoc}"
}

apply from: 'publish.gradle'
apply from: 'jacoco.gradle'

tasks.register('allJavadoc', Javadoc) {
    Set<String> projects = [
            'lua51',
            'lua52',
            'lua53',
            'lua54',
            'luajit',
            'luaj',
            'luajava',
    ]
    source subprojects
            .findAll { projects.contains(it.name) }
            .collect { it.sourceSets.main.allJava }
    classpath = files(subprojects.findAll { projects.contains(it.name) }
            .collect { it.sourceSets.main.compileClasspath })
    exclude(
            '**/party/iroiro/luajava/util/**',
            '**/party/iroiro/luajava/cleaner/**',
    )
    destinationDir = file("${buildDir}/docs/javadoc")
}
