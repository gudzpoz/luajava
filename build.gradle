group 'party.iroiro.luajava'
version '3.1.1-SNAPSHOT'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

subprojects { it ->
    if (it.name != 'android') {
        apply plugin: 'java'
        java {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
            withSourcesJar()
            withJavadocJar()
        }
    }
}

ext {
    jdk = JavaVersion.current().majorVersion
    jdkJavadoc = "https://docs.oracle.com/javase/$jdk/docs/api/"
    if (JavaVersion.current().isJava11Compatible()) {
        jdkJavadoc = "https://docs.oracle.com/en/java/javase/$jdk/docs/api/"
    }
    println "JDK Javadoc link for this build is ${rootProject.jdkJavadoc}"
}

apply from: 'publish.gradle'
apply from: 'jacoco.gradle'

task allJavadoc(type: Javadoc) {
    source subprojects.findAll {!['example', 'android'].contains(it.name) }.collect {
        it.sourceSets.main.allJava
    }
    classpath = files(subprojects.findAll {!['example', 'android'].contains(it.name) }.collect {
        it.sourceSets.main.compileClasspath
    })
    exclude '**/party/iroiro/luajava/util/**'
    destinationDir = file("${buildDir}/docs/javadoc")
}
